plugins {
    id 'swatch.java-library-conventions'
}

dependencies {
    // Intentionally marked as platform rather than enforcedPlatform as this project is a library
    implementation platform(libraries["quarkus-bom"])
    implementation 'org.yaml:snakeyaml'
    implementation 'org.slf4j:slf4j-api'
    implementation 'jakarta.validation:jakarta.validation-api'
    implementation 'com.google.guava:guava'
    implementation 'io.quarkus:quarkus-core'

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation libraries["hamcrest-all"]
}

tasks.register("generateYamlIndex") {
    outputs.file("$buildDir/index/swatch_config_index.txt")
    doLast {
        def sourceDir = layout.projectDirectory.dir("src/main/resources")
        def targetDir = "$buildDir/index"
        new File(targetDir).mkdir()
        def index = new File("$targetDir/swatch_config_index.txt")
        def paths = fileTree("$sourceDir/subscription_configs").files
                .collect {"${sourceDir.asFile.toPath().toAbsolutePath().relativize(it.toPath().toAbsolutePath())}" }
        index.write(String.join("\n", paths))
    }
}

processResources {
    from tasks.generateYamlIndex
}

description = 'SWATCH library for product configuration'
