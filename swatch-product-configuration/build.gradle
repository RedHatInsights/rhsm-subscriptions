plugins {
    id 'swatch.java-library-conventions'
}

dependencies {
    implementation libraries["snakeyaml"]
    implementation libraries["slf4j-api"]
    implementation libraries["jakarta-validation-api"]
    implementation libraries["guava"]

    testImplementation libraries["junit-jupiter"]
    testImplementation libraries["hamcrest-all"]
    testImplementation libraries["hibernate-validator"]
    testImplementation libraries["jakarta-el-impl"]
}

tasks.register("generateYamlIndex") {
    outputs.file("$buildDir/index/swatch_config_index.txt")
    doLast {
        def sourceDir = layout.projectDirectory.dir("src/main/resources")
        def targetDir = "$buildDir/index"
        new File(targetDir).mkdir()
        def index = new File("$targetDir/swatch_config_index.txt")
        def paths = fileTree("$sourceDir/subscription_configs").files
                .collect {"${sourceDir.asFile.toPath().toAbsolutePath().relativize(it.toPath().toAbsolutePath())}" }
        index.write(String.join("\n", paths))
    }
}

// Provide a hint to gradle that the yaml index must be rebuilt when any change happens in src/main/resources
tasks.generateYamlIndex.inputs.dir(layout.projectDirectory.dir("src/main/resources"))

processResources {
    from tasks.generateYamlIndex
}

description = 'SWATCH library for product configuration'
