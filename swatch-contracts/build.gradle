plugins {
    id 'swatch.quarkus-conventions'
    id 'org.openapi.generator'
    id 'jsonschema2pojo'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compileOnly libraries["lombok"]
    implementation 'io.quarkus:quarkus-hibernate-validator'
    implementation 'io.quarkus:quarkus-micrometer-registry-prometheus'
    implementation 'io.quarkus:quarkus-opentelemetry'
    implementation 'io.quarkus:quarkus-resteasy-jackson'
    implementation 'io.quarkus:quarkus-security'
    implementation 'io.quarkus:quarkus-smallrye-fault-tolerance'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation 'io.quarkus:quarkus-jdbc-postgresql'
    implementation 'io.quarkus:quarkus-messaging-amqp'
    implementation 'io.quarkus:quarkus-messaging-kafka'
    implementation 'io.smallrye.reactive:smallrye-reactive-messaging-in-memory'
    implementation project(':clients:quarkus:subscription-client')
    implementation project(':swatch-common-clock')
    implementation project(':swatch-common-config-workaround')
    implementation project(':swatch-common-kafka')
    implementation project(':swatch-common-resteasy')
    implementation project(':swatch-common-panache')
    implementation project(':swatch-common-smallrye-fault-tolerance')
    implementation project(':swatch-common-splunk')
    implementation project(':swatch-common-export')
    implementation project(':swatch-common-models')
    implementation project(':clients:quarkus:rbac-client')
    implementation project(':clients:quarkus:product-client')
    implementation project(':clients:quarkus:export-client')
    implementation project(':swatch-product-configuration')
    implementation project(':swatch-common-resteasy-client')
    implementation project(":clients:rh-partner-gateway-client")
    implementation project(':swatch-common-trace-response')
    implementation libraries["clowder-quarkus-config-source"]
    implementation libraries["quarkus-logging-splunk"]
    implementation libraries["splunk-library-javalogging"]
    implementation libraries["mapstruct"]
    // used to deserialize subscription/product messages from UMB
    implementation libraries["jackson-dataformat-xml"]
    implementation libraries["jackson-module-jakarta-xmlbind-annotations"]
    implementation libraries["jackson-datatype-jsr310"]
    annotationProcessor enforcedPlatform(libraries["quarkus-bom"])
    annotationProcessor "org.hibernate.orm:hibernate-jpamodelgen"
    annotationProcessor libraries["mapstruct-processor"]
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation libraries["junit-jupiter"]
    testImplementation libraries["wiremock"]
    testImplementation libraries["awaitility"]
    testImplementation("io.quarkus:quarkus-jdbc-h2")
    testImplementation("io.quarkus:quarkus-test-kafka-companion")
    testImplementation 'org.testcontainers:postgresql'
    testImplementation project(':swatch-common-testcontainers')

    // Run liquibase during tests
    testRuntimeOnly("io.quarkus:quarkus-liquibase")

    testRuntimeOnly(project(":swatch-database-migrations"))

    testAnnotationProcessor libraries["mapstruct-processor"]
    // if you are using mapstruct in test code
    annotationProcessor libraries["lombok-mapstruct-binding"]
}

compileJava.dependsOn tasks.openApiGenerate

openApiGenerate {
    generatorName = "java"
    inputSpec = "${projectDir}/src/main/resources/META-INF/openapi.yaml"
    outputDir = "${buildDir}/generated"
    apiPackage = "com.redhat.swatch.contract.openapi.resource"
    modelPackage = "com.redhat.swatch.contract.openapi.model"
    invokerPackage = "com.redhat.swatch.contract.openapi"
    groupId = "com.redhat.swatch.contract"
    configOptions = [sourceFolder     : "src/gen/java",
                     interfaceOnly    : "true",
                     library          : "microprofile",
                     java8            : "true",
                     dateLibrary      : "java8",
                     serializationLibrary: "jackson",
                     useBeanValidation: "true",
                     microprofileRestClientVersion: "3.0",
                     useJakartaEE: "true",
    ]
    additionalProperties = [disableMultipart: "true", // see https://github.com/OpenAPITools/openapi-generator/pull/4713#issuecomment-633906581
    ]
    importMappings = [
        "MetricId": "com.redhat.swatch.configuration.registry.MetricId",
        "ProductId": "com.redhat.swatch.configuration.registry.ProductId",
        "PartnerEntitlementV1": "com.redhat.swatch.clients.rh.partner.gateway.api.model.PartnerEntitlementV1"
    ]
    typeMappings = [
        "string+MetricId": "MetricId",
        "string+ProductId": "ProductId",
    ]
}

jsonSchema2Pojo {
    source = files("${projectDir}/../swatch-core/schemas/enabled_orgs_request.yaml",
            "${projectDir}/../swatch-core/schemas/enabled_orgs_response.yaml",
            "${projectDir}/schemas")
    targetPackage = "com.redhat.swatch.contract.model"
    targetDirectory = file("${buildDir}/generated/src/gen/java")
    includeAdditionalProperties = false
    includeJsr303Annotations = true
    initializeCollections = false
    dateTimeType = 'java.time.OffsetDateTime'
    sourceType = 'yamlschema'
    generateBuilders = true
    includeGetters = true
    includeSetters = true
    useJakartaValidation = true
}

sourceSets.main.java.srcDirs += ["${buildDir}/generated/src/gen/java"]
