// Plugin: swatch.quarkus-rest-client-conventions
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id "swatch.java-library-conventions"
    id "org.openapi.generator"
}

ext {
    api_spec_path = "${projectDir.toString().replace('quarkus/', '')}/${project.name.replace('-client', '-api-spec.yaml').replace(':quarkus', '')}"
    java_package = "com.redhat.swatch.clients.${project.name.replace(':quarkus', '').replace('-client', '').replaceAll('-', '.')}"
}

tasks.register("generateApiDocs", GenerateTask) {
    generatorName = "html"
    inputSpec = project.getExtensions().getByName("openApiGenerate").inputSpec
    outputDir = "$buildDir/docs"
    generateApiDocumentation = true
    generateModelDocumentation = true
    generateModelTests = false
    generateApiTests = false
    withXml = false
}

openApiGenerate {
    inputSpec = api_spec_path
    outputDir = "${buildDir}/generated"
    generatorName = "java"
    modelPackage = "${java_package}.api.model"
    apiPackage = "${java_package}.api.resources"
    invokerPackage = java_package
    configOptions = [
            generatePom: "false",
            library: "microprofile",
            dateLibrary: "java8",
            serializationLibrary: "jackson"
    ]
    additionalProperties = [
            disableMultipart: "true", // see https://github.com/OpenAPITools/openapi-generator/pull/4713#issuecomment-633906581
            // microprofile doesn't support the standard useJakartaEE=true
            rootJavaEEPackage: "jakarta",
    ]
}

dependencies {
    implementation enforcedPlatform(libraries["quarkus-bom"])
    api 'io.quarkus:quarkus-rest-client-reactive-jackson'
    api libraries["jackson-databind-nullable"]
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.quarkus:quarkus-junit5-mockito'
}

sourceSets.main.java.srcDirs += "${buildDir}/generated/src/main/java"

compileJava.dependsOn tasks.openApiGenerate
