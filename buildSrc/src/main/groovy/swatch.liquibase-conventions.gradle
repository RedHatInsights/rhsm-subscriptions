// Plugin: swatch.liquibase-conventions
// Applies liquibase gradle plugin to add some tasks for development
import org.liquibase.gradle.LiquibaseTask

plugins {
    id 'org.liquibase.gradle'
}

configurations {
    liquibaseRuntime.extendsFrom runtimeClasspath
}

liquibase {
    // force UTC time zone to be consistent with the app
    jvmArgs "-Duser.timezone=UTC"
    activities {
        main {
            changelogFile 'db/changeLog.xml'
            url 'jdbc:postgresql://localhost:5432/rhsm-subscriptions'
            username 'rhsm-subscriptions'
            password 'rhsm-subscriptions'
            logLevel "info"
        }
    }
}

dependencies {
    liquibaseRuntime enforcedPlatform(libraries["quarkus-bom"])
    liquibaseRuntime "info.picocli:picocli"
    liquibaseRuntime "org.liquibase:liquibase-core"
    liquibaseRuntime "org.postgresql:postgresql"
    liquibaseRuntime sourceSets.main.output
}

// custom task classes must be assembled into jars before liquibase can reference them
project.tasks.withType(LiquibaseTask.class).each {it.dependsOn "assemble"}
