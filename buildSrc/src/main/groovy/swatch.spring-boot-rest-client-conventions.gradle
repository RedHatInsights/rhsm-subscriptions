// Plugin: swatch.spring-boot-rest-client-conventions
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id "swatch.java-library-conventions"
    id "swatch.spring-boot-dependencies-conventions"
    id "org.openapi.generator"
}

ext {
    api_spec_path = "${projectDir}/${project.name.replace('-client', '-api-spec.yaml')}"
    config_file = "${projectDir}/${project.name}-config.json"
}

openApiValidate {
    inputSpec = api_spec_path
}

tasks.register("generateApiDocs", GenerateTask) {
    generatorName = "html"
    inputSpec = project.getExtensions().getByName("openApiGenerate").inputSpec
    outputDir = "$buildDir/docs"
    generateApiDocumentation = true
    generateModelDocumentation = true
    generateModelTests = false
    generateApiTests = false
    withXml = false
}

openApiGenerate {
    inputSpec = api_spec_path
    configFile = config_file
    outputDir = "${buildDir}/generated"
    generatorName = "java"
    configOptions = [
            generatePom: "false",
            library: "resteasy",
            java8: "true",
            dateLibrary: "java8",
            useJakartaEe: "true",
    ]
}

dependencies {
    api project(':clients-core')
    api "jakarta.annotation:jakarta.annotation-api"
    api (libraries["resteasy-client"]) {
        exclude group: "org.jboss", module: "jandex"
    }
    api (libraries["resteasy-multipart-provider"]) {
        exclude group: "org.jboss", module: "jandex"
    }

    api "com.fasterxml.jackson.core:jackson-annotations"
    api "com.fasterxml.jackson.core:jackson-databind"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"

    api "org.springframework:spring-context"
    api "org.slf4j:slf4j-api"
    api libraries["swagger-annotations"]
    api libraries["jsr305"]
    api libraries["jackson-databind-nullable"]

    compileOnly 'org.springframework.boot:spring-boot'
    testImplementation libraries["wiremock"]
    testImplementation libraries["guava"]
    // Necessary to test the serialization of JSON payloads
    testImplementation libraries["resteasy-jackson2-provider"]
}

sourceSets.main.java.srcDirs += "${buildDir}/generated/src/main/java"

compileJava.dependsOn tasks.openApiGenerate
