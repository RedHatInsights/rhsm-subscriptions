// Plugin: swatch.spring-boot-conventions
// gradle config common to any swatch component implemented as a spring boot app
plugins {
    id "swatch.java-conventions"
    id "swatch.spring-boot-dependencies-conventions"
    id "org.springframework.boot"
}

dependencies {
    // For the LiveReload feature of spring boot as long as IntelliJ is set to build/make automatically on
    // code changes
    implementation "org.springframework.boot:spring-boot-devtools"
    // This starter pulls in Spring Boot versions that we don't want.  The actual classes that we need are
    // baked into the starter artifact itself which is peculiar but sometimes that's how the cookie crumbles.
    implementation(libraries["resteasy-spring-boot-starter"]) {
        exclude group: "org.springframework.boot"
        exclude group: "org.jboss", module: "jandex"
    }
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.retry:spring-retry"
    implementation "org.springframework:spring-context-support"
    implementation "io.micrometer:micrometer-registry-prometheus"
    runtimeOnly(libraries["resteasy-validator-provider"]) {
        exclude group: "org.hibernate" // exclude older hibernate validator
    }
    implementation libraries["swagger-ui"]
    implementation libraries["webjars-locator"]
    implementation "org.hibernate.validator:hibernate-validator"
    implementation "org.yaml:snakeyaml"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework:spring-test"
}

// Disable the default jar; otherwise we end up with two different jars in the final image
// See https://docs.spring.io/spring-boot/docs/2.5.0/gradle-plugin/reference/htmlsingle/#packaging-executable.and-plain-archives
tasks.jar {
    enabled = false
}

springBoot {
    mainClass = "org.candlepin.subscriptions.BootApplication"
    buildInfo {
        properties {
            // Leave time empty: The default value is the instant at which the project is built. A
            // side-effect is the task will never be up-to-date. As a result, builds will take longer.  See
            // https://docs.spring.io/spring-boot/docs/2.1.4.RELEASE/gradle-plugin/reference/html/#integrating-with-actuator
            time = null
        }
    }
}
