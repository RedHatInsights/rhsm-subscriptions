import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:4.0.0-beta"
    }
}

plugins {
    id "checkstyle"
    id "jacoco"
    id "org.sonarqube" version "2.7"
    id "org.springframework.boot" version "2.0.8.RELEASE"
}

ext {
    swagger_annotations_version = "1.5.21"
    jackson_version = "2.8.11"
    jackson_databind_version = "2.8.11.2"
    resteasy_version = "3.6.2.Final"
    spring_version = "5.1.4.RELEASE"
    spring_boot_version = "2.1.2.RELEASE"
    junit_version = "5.3.2"
    slf4j_version = "1.7.25"
    mockito_version = "2.23.4"
}

allprojects {
    apply plugin: "java"
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    checkstyle {
        toolVersion "8.16"
    }

    group = "org.candlepin"

    dependencies {
        testCompile "org.springframework.boot:spring-boot-test:$spring_boot_version"
        testCompile "org.springframework:spring-test:$spring_version"
        testCompile "org.junit.jupiter:junit-jupiter-api:$junit_version"
        testCompile "org.mockito:mockito-core:$mockito_version"
        testCompile "org.mockito:mockito-junit-jupiter:$mockito_version"
        testCompile "org.hamcrest:hamcrest:2.1"

        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_version"
        testRuntime "ch.qos.logback:logback-classic:1.2.3"
    }
}

version = "1.0.0"

bootJar {
    mainClassName = "org.candlepin.insights.BootApplication"
}

processResources {
    filesMatching("**/application.yaml") {
        // Only expand ${version}.  We don't want to conflict with Spring expansions.
        filter(ReplaceTokens, tokens: [version: version])
    }
}

dependencies {
    // Generates configuration metadata that IntelliJ can use
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:$spring_boot_version"

    compile project(":api")
    compile project(":insights-inventory-client")
    compile project(":pinhead-client")
    compile "javax.servlet:javax.servlet-api:3.1.0"
    compile "org.jboss.resteasy:resteasy-spring-boot-starter:3.0.0.Final"
    compile "org.jboss.spec.javax.ws.rs:jboss-jaxrs-api_2.1_spec:1.0.2.Final"
    compile "org.springframework.boot:spring-boot-actuator-autoconfigure:$spring_boot_version"
    compile "org.springframework.boot:spring-boot-autoconfigure:$spring_boot_version"
    compile "org.springframework.boot:spring-boot:$spring_boot_version"
    compile "org.springframework:spring-beans:$spring_version"
    compile "org.springframework:spring-context:$spring_version"
    compile "org.springframework:spring-context-support:$spring_version"
    compile "org.springframework:spring-jdbc:$spring_version"
    compile "org.springframework:spring-tx:$spring_version"
    compile "org.springframework:spring-webmvc:$spring_version"
    compile "org.slf4j:slf4j-api:$slf4j_version"
    compile "net.logstash.logback:logstash-logback-encoder:5.3"
    compile "io.micrometer:micrometer-registry-prometheus:1.1.2"
    compile "org.webjars:swagger-ui:3.20.8"
    compile("org.quartz-scheduler:quartz:2.3.0") {
        // These excludes are present in the spring-boot-starter-quartz POM file but we don't want to use
        // the starter since it pulls in some other items
        exclude group: "com.mchange", module: "c3p0"
        exclude group: "com.zaxxer", module: "HikariCP-java6"
    }

    // Runtime deps that will be included in the result package but not on the compile classpath.  I.e.
    // implementations of APIs we are using.
    runtime "ch.qos.logback:logback-classic:1.2.3"
    runtime "org.jboss.resteasy:resteasy-jaxrs:$resteasy_version"
    runtime "org.jboss.resteasy:resteasy-validator-provider-11:$resteasy_version"
    runtime "org.hibernate.validator:hibernate-validator:6.0.14.Final"
    runtime "org.hsqldb:hsqldb:2.4.1"
    runtime "org.apache.tomcat:tomcat-jdbc:9.0.16"
    runtime "org.postgresql:postgresql:42.2.2"
}

compileJava.dependsOn(processResources)

project(":api") {
    apply plugin: "org.openapi.generator"

    // This project should only contain generated code.  No point in running checkstyle
    checkstyle.sourceSets = []

    ext {
        api_spec_path = "${projectDir}/rhsm-conduit-api-spec.yaml"
        config_file = "${projectDir}/rhsm-conduit-api-config.json"
    }

    openApiGenerate {
      generatorName = "jaxrs-spec"
      inputSpec = api_spec_path
      configFile = config_file
      outputDir = "$buildDir/generated"
      configOptions = [
          interfaceOnly: true,
          generatePom: false
      ]
    }

    openApiValidate {
        inputSpec = api_spec_path
    }

    task generateApiDocs(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        generatorName = "html"
        inputSpec = api_spec_path
        outputDir = "$buildDir/docs"
        generateApiDocumentation = true
        generateModelDocumentation = true
        generateModelTests = false
        generateApiTests = false
        withXml = false
    }

    task generateOpenApiJson(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        generatorName = "openapi"
        inputSpec = api_spec_path
        outputDir = "$buildDir/generated"
        generateApiDocumentation = true
        generateModelDocumentation = true
        generateModelTests = false
        generateApiTests = false
        withXml = false
    }

    processResources {
        from "$buildDir/generated/openapi.json"
        from api_spec_path
        rename { String fileName ->
            api_spec_path.endsWith(fileName) ? 'openapi.yaml' : fileName  // rename yaml to openapi.yaml
        }
    }

    dependencies {
        compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.8'
        compile 'javax.validation:validation-api:2.0.1.Final'
        compile 'org.jboss.spec.javax.ws.rs:jboss-jaxrs-api_2.1_spec:1.0.2.Final'
        compile 'io.swagger:swagger-annotations:1.5.21'
    }

    sourceSets.main.java.srcDirs = ["${buildDir}/generated/src/gen/java"]
    compileJava.dependsOn tasks.openApiGenerate
    processResources.dependsOn tasks.generateOpenApiJson
}

project(":insights-inventory-client") {
    apply plugin: "org.openapi.generator"

    ext {
        api_spec_path = "https://raw.githubusercontent.com/RedHatInsights/insights-host-inventory/master/swagger/api.spec.yaml"
        config_file = "${projectDir}/insights-inventory-client-config.json"
    }
}

project(":pinhead-client") {
    apply plugin: "org.openapi.generator"

    ext {
        api_spec_path = "${projectDir}/pinhead-client-spec.yaml"
        config_file = "${projectDir}/pinhead-client-config.json"
    }

    dependencies {
        testCompile "com.github.tomakehurst:wiremock-jre8:2.21.0"
    }
}

configure(subprojects.findAll { it.name == 'insights-inventory-client' || it.name == 'pinhead-client'}) {
    apply plugin: "org.openapi.generator"
    apply plugin: "checkstyle"

    openApiValidate {
        inputSpec = api_spec_path
    }

    task generateApiDocs(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        generatorName = "html"
        inputSpec = api_spec_path
        outputDir = "$buildDir/docs"
        generateApiDocumentation = true
        generateModelDocumentation = true
        generateModelTests = false
        generateApiTests = false
        withXml = false
    }

    openApiGenerate {
        inputSpec = api_spec_path
        configFile = config_file
        outputDir = "${buildDir}/generated"
        generatorName = "java"
        configOptions = [
            generatePom: false,
            library: "resteasy",
            java8: true,
            dateLibrary: "java8"
        ]
    }

    dependencies {
        compile "org.jboss.resteasy:resteasy-client:$resteasy_version"
        compile "org.jboss.resteasy:resteasy-multipart-provider:$resteasy_version"

        compile "com.fasterxml.jackson.core:jackson-databind:$jackson_databind_version"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson_version"
        compile "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"

        compile "org.springframework:spring-context:$spring_version"
        compile "org.slf4j:slf4j-api:$slf4j_version"
        compile "io.swagger:swagger-annotations:$swagger_annotations_version"
    }

    sourceSets.main.java.srcDirs += "${buildDir}/generated/src/main/java"
    compileJava.dependsOn tasks.openApiGenerate
}
